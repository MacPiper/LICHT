
(* @NESTEDCOMMENTS := 'Yes' *)
(* @PATH := '\/Funktionsbloecke\/Lichtblöcke' *)
(* @OBJECTFLAGS := '0, 8' *)
(* @SYMFILEFLAGS := '2048' *)
FUNCTION_BLOCK LICHT
VAR_INPUT
	TASTER : BOOL;
	AUS_SOFORT : BOOL;
	AUS_MIT_ANGEKUENDIGUNG: BOOL;
	RETRIGGER_ON_TIME : BOOL;
	EIN : BOOL;
	INHIBIT_AUTO_OFF: BOOL;
END_VAR
VAR_INPUT CONSTANT
	ON_TIME:TIME := t#30m;
	ON_TIME_DAUER:TIME := t#12h;
	DOUBLE_CLICK_DAUER_ACTIVATION:BOOL := FALSE;
	AFTERRUN_TIME:TIME := t#15s;
	AFTERRUN_BLINK_LOW:TIME:= t#500ms;
	AFTERRUN_BLINK_HIGH:TIME := t#1500ms;
	AFTERRUN_NUMBER_OF_PULSES : BYTE := 3;
	RETRIGGER_ON_TIME_AS_STATE : BOOL := FALSE; (* set to true if the input state should be evaluated rather than the rising edge *)
END_VAR
VAR_OUTPUT
	POWER:BOOL;
	POWER_NO_BLINK:BOOL;
END_VAR
VAR
	zustand_en:lichtZustand_ten :=LICHT_ZUSTAND_AUS;
	tasterPosEdge : R_TRIG;
	tasterLongPress : TON;
	retriggerOnTimeEdge : R_TRIG;
 	einPosEdge : R_TRIG;
	einLongPress : TON;
	ausSofortEdge : R_TRIG;
	ausMitAnkuendigungEdge : R_TRIG;
	doubleClickTimer : TP;

	tonTimer : TON;
      afterrunTimer : TON;

	blinker : BLINK;
	blinkCounter : CTU := (CU:=TRUE, RESET:=TRUE, M:=TRUE); (* without this initialisation it would detect a pos. edge at first cycle with CU = TRUE *)
END_VAR
(* @END_DECLARATION := '0' *)
tasterPosEdge(CLK := TASTER );
tasterLongPress(IN := TASTER, PT := t#500ms );

retriggerOnTimeEdge(CLK := RETRIGGER_ON_TIME);
einPosEdge(CLK := EIN );
einLongPress(IN := EIN, PT := t#500ms );
ausSofortEdge(CLK := AUS_SOFORT );
ausMitAnkuendigungEdge(CLK := AUS_MIT_ANGEKUENDIGUNG );
afterrunTimer(IN := (LICHT_ZUSTAND_AUSSCHALT_WARNUNG = zustand_en), PT := AFTERRUN_TIME );

IF (TRUE = RETRIGGER_ON_TIME) THEN
	IF ( (TRUE = retriggerOnTimeEdge.Q) OR (TRUE = RETRIGGER_ON_TIME_AS_STATE) ) THEN
		tonTimer(IN := 0, PT := ON_TIME );
	END_IF
ELSIF (LICHT_ZUSTAND_EIN_ZEITBEGRENZT = zustand_en) THEN
	 tonTimer(IN := 1, PT := ON_TIME );
	doubleClickTimer(IN := 1, PT := t#500ms );
ELSE
	tonTimer(IN := POWER, PT := ON_TIME_DAUER );
	doubleClickTimer(IN := 0, PT := t#500ms );
END_IF



CASE (zustand_en) OF
	LICHT_ZUSTAND_AUS:
		IF( (TRUE = tasterPosEdge.Q) OR (TRUE = einPosEdge.Q) ) THEN
			(*  Switch on via positive edge on input TASTER or EIN *)
			zustand_en := LICHT_ZUSTAND_EIN_ZEITBEGRENZT;
		END_IF
		POWER := FALSE;
		POWER_NO_BLINK := FALSE;

	LICHT_ZUSTAND_EIN_ZEITBEGRENZT:
		IF (TRUE = einPosEdge.Q) THEN
			tonTimer(IN := 0, PT := ON_TIME );
		ELSIF ( (TRUE = tasterPosEdge.Q) OR (TRUE = ausSofortEdge.Q) ) THEN
			IF ((TRUE=doubleClickTimer.Q) AND (TRUE=DOUBLE_CLICK_DAUER_ACTIVATION))THEN
				zustand_en := LICHT_ZUSTAND_EIN_DAUER;
			ELSE
				zustand_en := LICHT_ZUSTAND_AUS;
			END_IF
		ELSIF ((TRUE = tasterLongPress.Q)  OR (TRUE = einLongPress.Q) OR (t#0s = ON_TIME)) THEN
			zustand_en := LICHT_ZUSTAND_EIN_DAUER;
		ELSIF (  ( (TRUE = tonTimer.Q) AND (FALSE = INHIBIT_AUTO_OFF ) )OR (TRUE = ausMitAnkuendigungEdge.Q)  ) THEN
			zustand_en := LICHT_ZUSTAND_AUSSCHALT_WARNUNG;
		END_IF
		POWER := TRUE;
		POWER_NO_BLINK := TRUE;

	LICHT_ZUSTAND_EIN_DAUER:
		IF( (TRUE = tasterPosEdge.Q) OR (TRUE = ausSofortEdge.Q) ) THEN
			zustand_en := LICHT_ZUSTAND_AUS;
		ELSIF (  ( (TRUE = tonTimer.Q) AND (FALSE = INHIBIT_AUTO_OFF ) )OR (TRUE = ausMitAnkuendigungEdge.Q)  ) THEN
			zustand_en := LICHT_ZUSTAND_AUSSCHALT_WARNUNG;
		END_IF
		POWER := TRUE;
		POWER_NO_BLINK := TRUE;

	LICHT_ZUSTAND_AUSSCHALT_WARNUNG:
		(* Leaving afterrun with EIN pulse is probably a bad idea as it is not visible but what's the option? *)
		IF ((TRUE = tasterPosEdge.Q) OR (TRUE = einPosEdge.Q) OR (TRUE = retriggerOnTimeEdge.Q)) THEN
			tonTimer(IN := 0);
			blinker(ENABLE:=FALSE);
			blinkCounter(RESET:=TRUE);

			zustand_en := LICHT_ZUSTAND_EIN_ZEITBEGRENZT;
			POWER := TRUE;
			POWER_NO_BLINK := TRUE;
		ELSIF ( ( TRUE = afterrunTimer.Q ) OR (TRUE =ausSofortEdge.Q) )THEN
			blinker(ENABLE:=FALSE);
			blinkCounter(RESET:=TRUE);

			zustand_en := LICHT_ZUSTAND_AUS;
			POWER := FALSE;
			POWER_NO_BLINK := FALSE;
		ELSE
			blinker(ENABLE:=TRUE,TIMELOW:=AFTERRUN_BLINK_LOW, TIMEHIGH:=AFTERRUN_BLINK_HIGH);
			blinkCounter(CU:=POWER,RESET:=FALSE,PV:=AFTERRUN_NUMBER_OF_PULSES);
			POWER := blinker.out OR blinkCounter.Q;
			POWER_NO_BLINK := TRUE;
		END_IF

END_CASE;

(* Author: MacPiper
Changelog:
12.05.20 ver 1.4 Added functionality for longtime-activation through doubleclick (set parameter DOUBLE_CLICK_DAUER_ACTIVATION to TRUE to enable it)
26.04.17 ver 1.3 Added parameter RETRIGGER_ON_TIME_AS_STATE. When set, the timer starts to run only when the input is RETRIGGER_ON_TIME low
03.12.16 ver 1.2. Make blinking in afterrun configurable (AFTERRUN_BLINK_LOW; AFTERRUN_BLINK_HIGH, AFTERRUN_NUMBER_OF_PULSES)
24.10.13 ver 1.1. Allow retrigger of on-time in afterrun state
*)
END_FUNCTION_BLOCK
